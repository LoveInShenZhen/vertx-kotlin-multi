apply plugin: 'application'

dependencies {
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.58'

    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'

    compile project(':common_lib')
    compile project(':models')
    compile files("conf")
}

distZip {
    into(project.name) {
        from '.'
        include 'conf/*'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'conf/*'
    }
}

mainClassName = 'com.appServer.ApiServer'

tasks.withType(CreateStartScripts) {
    it.unixStartScriptGenerator.template = resources.text.fromFile('unixStartScript.txt')
}

tasks.withType(JavaExec) {
    if (project.hasProperty("debug")) {
        def debugPort = "5005"
        if (project.findProperty("debug").toString().isNumber()) {
            debugPort = project.findProperty("debug").toString()
        }
        it.jvmArgs.add("-Xdebug")
        it.jvmArgs.add("-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=${debugPort}")
        println("\u001b[35m" + "Remote debug is enabled. debug port: ${debugPort}" + "\u001b[0m")
    }

    it.systemProperties(System.properties as Map<String, ?>)
}
